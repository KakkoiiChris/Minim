; Brainfuck Interpreter \
  Christian Alexander   \
  07/18/2021
  
; Memory Space Guide            \
  0: End of BF source           \
  1: Instruction pointer        \
  2: Cell pointer               \
  3: Loop level                 \
  4 -> [0]: BF Source           \
  [0] -> [0] + 30000: BF Memory

; NOTE: There are no negative cells in this implementation.

; BF source is passed as a command line argument.

; Hello World\
  ++++++++[>++++[>++>+++>+++>+<<<<-]>+>+>->>+[<]<-]>>.>---.+++++++..+++.>>.<-.<.+++.------.--------.>>+.>++.
  
; Truth-machine\
  >>>,.[[->+<<+>]>-]<<<[<<]>[.]

; Queue command line args system call
[1 :] = "args".
\< 1.

; Source starts at index 4
[0] = 4.

; Insert args into memory
_> 1.
    \> [[0]].
    
    _< ?[[0]++].
_> 0.

; Set indices 1-3 to 0
[1 @ 3] = 0.

; Begin loop
_> '('.
    ; Goto that ignores all non-command characters
    _< [[1] + 4] == '+' || [[1] + 4] == '-' || [[1] + 4] == '<' || [[1] + 4] == '>' || [[1] + 4] == '[' || [[1] + 4] == ']' || [[1] + 4] == '.' || [[1] + 4] == ',' ? [[1] + 4] : ' '.
    
    ; + Command
    _> '+'.
        [[2] + [0]]++.
        
        _^ [[2] + [0]] < 256.
            [[2] + [0]] = 0.
    _< ' '.
    
    ; - Command
    _> '-'.
        [[2] + [0]]--.
        
        _^ [[2] + [0]] > -1.
            [[2] + [0]] = 255.
    _< ' '.
    
    ; - Command
    _> '<'.
        [2]--.
        
        _^ [2] > -1.
            [2] = 29999.
    _< ' '.
    
    ; - Command
    _> '>'.
        [2]++.
        
        _^ [2] < 30000.
            [2] = 0.
    _< ' '.
    
    ; [ Command
    _> '['.
        [3] = 0.
        [3]++.
        
        _< [[2] + [0]] == 0 ? 'A' : 'a'.
        
        _> 'A'.
            [1]++.
        
            _^ [[1] + 4] <> '['.
                [3]++.
            
            _^ [[1] + 4] <> ']'.
                [3]--.
            
            _^ [1] < [0] - 1.
                [1] = [0] - 1.
            
            _< [[1] + 4] == ']' && ![3] ? 'a' : 'A'.
        _> 'a'.
    _< ' '.
    
    ; ] Command
    _> ']'.
        [3] = 0.
        [3]--.
    
        _< [[2] + [0]] <> 0 ? 'B' : 'b'.
        
        _> 'B'.
            [1]--.
        
            _^ [[1] + 4] <> '['.
                [3]++.
            
            _^ [[1] + 4] <> ']'.
                [3]--.
            
            _^ [1] > -1.
                [1] = 0.
            
            _< [[1] + 4] == '[' && ![3] ? 'b' : 'B'.
        _> 'b'.
    _< ' '.
    
    ; . Command
    _> '.'.
        $< [[2] + [0]].
    _< ' '.
    
    ; , Command
    _> ','.
        $> [[2] + [0]].
    _< ' '.
    
    _> ' '.
    
    [1]++.
    
    _< ?[[1] + 4] ? '(' : ')'.
_> ')'.