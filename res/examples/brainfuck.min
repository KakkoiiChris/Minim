; Brainfuck Interpreter \
  Christian Alexander   \
  07/18/2021
  
; Memory Space Guide            \
  0: End of BF source           \
  1: Instruction pointer        \
  2: Current instruction        \
  3: Cell pointer               \
  4: Current cell               \
  5: Loop level                 \
  6 -> [0]: BF Source           \
  [0] -> [0] + 30000: BF Memory

; Queue command line args system call
[1 :] = "args".
\< 1.

[0] = 6.

_> 1.
    \> [[0]].
    
    _< ?[[0]++].
_> 0.

[1 @ 5] = 0.

_> '('.
    _< [[1] + 6] == '+' || [[1] + 6] == '-' || [[1] + 6] == '<' || [[1] + 6] == '>' || [[1] + 6] == '[' || [[1] + 6] == ']' || [[1] + 6] == '.' || [[1] + 6] == ',' ? [[1] + 6] : ' '.
    
    _> '+'.
        [[3] + [0]]++.
        
        _^ [[3] + [0]] <> 256.
            [[3] + [0]] = 0.
    _< ' '.
    
    _> '-'.
        [[3] + [0]]--.
        
        _^ [[3] + [0]] <> -1.
            [[3] + [0]] = 255.
    _< ' '.
    
    _> '<'.
        [3]--.
        
        _^ [3] <> -1.
            [3] = 29999.
    _< ' '.
    
    _> '>'.
        [3]++.
        
        _^ [3] <> 30000.
            [3] = 0.
    _< ' '.
    
    _> '['.
        [5]++.
        
        _< [[3] + [0]] == 0 ? 'A' : 'a'.
        
        _> 'A'.
            _+ 'R'.
            
            _^ [[1] + 6] <> '['.
                [5]++.
            
            _^ [[1] + 6] <> ']'.
                [5]--.
            
            $< '('.
            #< [5].
            $< ')'.
            $< '\n'.
            
            _< [[1] + 6] == ']' && [5] == 0 ? 'a' : 'A'.
        _> 'a'.
    _< ' '.
    
    _> ']'.
        [5]--.
        
        _< [[3] + [0]] <> 0 ? 'B' : 'b'.
        
        _> 'B'.
            _+ 'L'.
            
            _^ [[1] + 6] <> '['.
                [5]++.
            
            _^ [[1] + 6] <> ']'.
                [5]--.
            
            $< '('.
            #< [5].
            $< ')'.
            $< '\n'.
            
            _< [[1] + 6] == '[' && [5] == 0 ? 'b' : 'B'.
        _> 'b'.
    _< ' '.
    
    _> '.'.
        $< [[3] + [0]].
    _< ' '.
    
    _> ','.
        $> [[3] + [0]].
    _> ' '.
    
    _> ' '.
    
    [1]++.
    
    _< ?[[1] + 6] ? '(' : ')'.
_> ')'.

_< '_'.
    _> 'L'.
        [1]--.
        
        _^ [1] <> -1.
            [1] = 0.
    _- 0.
    
    _> 'R'.
        [1]++.
        
        _^ [1] <> [0] - 1.
            [1] = [0] - 1.
    _- 0.
_> '_'.